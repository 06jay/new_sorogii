-	script	KoE	-1,{
OnInit:
disablenpc "The King#KoE";
disablenpc "Exit#KoE";
end;

OnWhisperGlobal:
if ( getgroupid() < 99 ) end;
else if ( compare ( @whispervar0$, "on" ) ) goto L_start;
else if ( compare ( @whispervar0$, "off" ) ) goto L_end;
else end;

L_end:
announce "The King of Emperium Hill is over!", 0;
set .koe_start, 0;
enablenpc "Exit#KoE";
disablenpc "The King#KoE";
killmonsterall "guild_vs1";
end;

OnClock1930:
OnClock2100:
//etc
if( gettime(4) != 1 && gettime(4) != 3 && gettime(4) != 5 && gettime(4) != 0 ) end;

L_start:
announce "The King of Emperium Hill has begun!", 0;
set .koe_start, 1;
enablenpc "The King#KoE";
disablenpc "Exit#KoE";
set $koegid, 0;
donpcevent "::OnRevKoE";
maprespawnguildid "guild_vs1", $koegid, 6;
monster "guild_vs1",49,49,"EMPERIUM",1288,1,"KoE::OnEmpDead";
end;

OnEmpDead:
set $koegid, getcharid(2);
announce "The current King of Emperium Hill is the [" + strcharinfo(2) + "] guild.", 0;
donpcevent "::OnRevKoE";
maprespawnguildid "guild_vs1", $koegid, 6;
sleep 500;
if ( .koe_start )
	monster "guild_vs1",49,49,"EMPERIUM",1288,1,"KoE::OnEmpDead";
end;
}

// KoE Entrance
prontera,119,158,4	script	The King#KoE	58,{
   mes "[The King]";
if ( getcharid(2) == 0 ) {
	mes "You must have a guild to participate in the ^FF0000King of Emperium Hill Tournament^000000.";
	close;
}
   mes "Hello.";
   mes "Would you like to participate in the ^FF0000King of Emperium Hill Tournament^000000?";
if ( select ( "Yes", "No" ) == 2 ) close;
   switch( rand(1,4) ){
       case 1:	warp "guild_vs1", 50, 88; end;
       case 2:	warp "guild_vs1", 88, 50; end;
       case 3:	warp "guild_vs1", 50, 11; end;
       case 4:	warp "guild_vs1", 11, 50; end;
}
}

// KoE Exit
guild_vs1,49,56,5	script	Exit#KoE	51,{
mes "[Exit]";
mes "See ya.";
if ( getcharid(2) == $koegid )
getitem 501, 1; // configure prize here
set Zeny,Zeny + 50000000;
next;
warp "Save",0,0;
close;
}

// Flags
guild_vs1,49,38,4	script	King of Emperium Hill#1::koe_flag	722,{
set .@gid, $koegid;
if ( .@gid == 0 ) end;
mes "[King of Emperium Hill]";
mes "The Current King of Emperium Hill is the ["+ getguildname(.@gid) +"] guild.";
close;
OnRevKoE:
flagemblem $koegid;
end;
}
guild_vs1,61,49,6	duplicate(koe_flag)	King of Emperium Hill#2	722
guild_vs1,38,49,2	duplicate(koe_flag)	King of Emperium Hill#3	722
guild_vs1,49,61,0	duplicate(koe_flag)	King of Emperium Hill#4	722

guild_vs1	mapflag	gvg
guild_vs1	mapflag	nobranch
guild_vs1	mapflag	nomemo
guild_vs1	mapflag	nopenalty
guild_vs1	mapflag	noreturn
guild_vs1	mapflag	nosave	SavePoint
guild_vs1	mapflag	noteleport
guild_vs1	mapflag	gvg_noparty
guild_vs1	mapflag	nowarp
guild_vs1	mapflag	nowarpto
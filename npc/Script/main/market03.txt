//===== rAthena Script =====================================
//= Vendor Control
//===== By: ================================================
//= Original by Emistry
//= Rewrite by AnnieRuru
//===== Current Version: ===================================
//= 2.1r
//===== Compatible With: ===================================
//= rAthena 2019-2-10
//===== Description: =======================================
//= Vendors only vend on certain coordinate, looks tidy :P
//===== Topic ==============================================
//= https://rathena.org/board/topic/117737-error-vendinghelp/
//===== Additional Comments: ===============================
//= Revert back to use warp
//==========================================================

prontera,144,230,6	script	Market Manager#02	726 ,{
	mes "["+ strnpcinfo(NPC_NAME_VISIBLE) +"]";
	if ( !getskilllv( "MC_VENDING" ) ) {
		mes "You dont have ^0055FFVending Skill^000000.";
		close;
	}
	mes "Select a slot and start vending.";
	while ( true ) {
		.@menu$ = .@currentpage ? "Previous Page:" : ":";
		.@pageindex = .@currentpage * .slotperpage;
		for ( .@i = 0; .@i < .slotperpage && .@pageindex + .@i < .count; ++.@i ) {
			if ( !getmapxy( .@map$, .@x, .@y, BL_NPC, "#vend_slot"+( .@pageindex + .@i ) ) && getareausers( .map$, .@x, .@y, .@x, .@y ) )
				.@menu$ += "Slot No."+( .@pageindex + .@i +1 )+" - ^FF0000[Unavailable]^000000:";
			else
				.@menu$ += "Slot No."+( .@pageindex + .@i +1 )+" - ^00CC00[Available]^000000:";
		}
		if ( .@currentpage != .maxpage )
			.@menu$ = .@menu$ + "Next Page";
		.@pick = select( .@menu$ ) -1;
		if ( !.@pick )
			{ --.@currentpage; }
		else if ( .@pick == .slotperpage +1 )
			{ ++.@currentpage; }
		else
			break;
	}
	.@s =  .@currentpage * .slotperpage + .@pick -1 ;
	if ( !getmapxy( .@map$, .@x, .@y, BL_NPC, "#vend_slot"+ .@s ) && getareausers( .map$, .@x, .@y, .@x, .@y ) ) {
		mes " ";
		mes "This slot is ^FF0000[UnAvailable]^000000.";
		close;
	}
	warp .@map$, .@x, .@y;
	hideonnpc "#vend_slot"+ .@s;
	donpcevent "#vend_slot"+ .@s +"::OnCheckAvailable";
	end;
OnInit:
	setbattleflag "min_npc_vendchat_distance", 0;
//	======================= Configuration HERE =============================
	.map$ = "prontera"; // map
	setarray .@x, 112,115,103; // x coordinates
	setarray .@y, 190,184; // y coordinates
	.slotperpage = 20; // display how many slots per page (prevent making the menu too long)

	freeloop true;
	setcell .map$, 0,0, 500,500, cell_novending, true;
	.@x_size = getarraysize( .@x );
	.@y_size = getarraysize( .@y );
	for ( .@i = 0; .@i < .@y_size; ++.@i ) {
		for ( .@j = 0; .@j < .@x_size; ++.@j ) {
			if ( checkcell( .map$, .@x[.@j], .@y[.@i], cell_chkpass ) ) {
				movenpc "#vend_slot"+ .count, .@x[.@j], .@y[.@i];
				setcell .map$, .@x[.@j], .@y[.@i], .@x[.@j], .@y[.@i], cell_novending, false;
				if ( getareausers( .map$, .@x[.@j], .@y[.@i], .@x[.@j], .@y[.@i] ) ) {
					hideonnpc "#vend_slot"+ .count;
					donpcevent "#vend_slot"+ .count +"::OnCheckAvailable";
				}
				++.count;
			}
		}
	}
	.maxpage = .count / .slotperpage + ( .count % .slotperpage > 0 ) -1;
//	debugmes "Vendor NPC needed -> "+ .count;
	end;
}

-	script	vend_slot	FAKE_NPC,{
	mes "[Vending Slot]";
	if ( !getskilllv( "MC_VENDING" ) ) {
		mes "You dont have ^0055FFVending Skill^000000.";
		close;
	}
	getmapxy .@map$, .@x, .@y, BL_NPC;
	if ( getareausers( .@map$, .@x, .@y, .@x, .@y ) ) {
		mes "Someone already using this slot";
		close;
	}
//	unitwalk getcharid(3), .@x, .@y;
	warp strcharinfo(3), .@x, .@y;
	mes "Spot : ^777777Available^000000";
	mes "^0055FF[ Vending Area Rules ]^000000";
	mes "^FF0000 > Use proper Shop Name.^000000";
	mes "^FF0000 > You can use @auto Trade.^000000";
	mes "^FF0000 > All item Have VAT.^000000";
	mes "^FF0000 > Happy Selling.^000000";
	hideonnpc strnpcinfo(0);
	donpcevent strnpcinfo(0)+"::OnCheckAvailable";
	close;
OnCheckAvailable:
	sleep 10000;
	getmapxy .@map$, .@x, .@y, BL_NPC;
	while ( getareausers( .@map$, .@x, .@y, .@x, .@y ) )
		sleep 10000;
	hideoffnpc strnpcinfo(0);
	end;
}



// Add more if needed.
prontera,0,0,4	duplicate(vend_slot)	#vend_slot81	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot82	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot83	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot84	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot85	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot86	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot87	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot88	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot89	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot90	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot91	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot92	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot93	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot94	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot95	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot96	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot97	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot98	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot99	4_BULLETIN_BOARD2
prontera,0,0,4	duplicate(vend_slot)	#vend_slot100	4_BULLETIN_BOARD2





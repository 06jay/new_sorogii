-	shop	custom_seller2	-1,501:500
izlude,88,134,5	script	PVP Points Dealer	469,{

	mes "[PK Points Dealer]";
	mes "Trade your points here!";
	next;
	mes "[PK Points Dealer]";
	mes "You currently have [^FF0000"+pk_points+"^000000] PK points to spend.";
	callshop "custom_seller2",1;
	npcshopattach "custom_seller2";
	end;
	
OnBuyItem:
	for(set .@i,0; .@i<getarraysize(.customs); set .@i,.@i+1) {
		for(set .@d,0; .@d<getarraysize(@bought_nameid); set .@d,.@d+1) {
			if(@bought_nameid[.@d]==.customs[.@i]) {
				if(checkweight(@bought_nameid[.@d],@bought_quantity[.@d])) {
					if(pk_points >= .Price[.@i]*@bought_quantity[.@d]) {
						set pk_points, pk_points - .Price[.@i]*@bought_quantity[.@d];
						getitem @bought_nameid[.@d],@bought_quantity[.@d];
					} else dispbottom "You don't have enough PK points to purchase that item.";


				} else dispbottom "Purchasing these items will put you over the weight limit!";
			}
		}
	}
	deletearray @bought_quantity, getarraysize(@bought_quantity);
	deletearray @bought_nameid, getarraysize(@bought_nameid);
	end;

OnPCKillEvent:
	if( strcharinfo(3) != .map$ ) end;
	copyarray( @killed$[1], @killed$[0], .saved_l-1 );
	@killed$ = ""+killedrid;
	if( countstr( implode( @killed$, "|" ), ""+killedrid, 0 ) <= .rstrk_l ) {
		pk_points++;
		dispbottom "You've gained one point! Your total is ["+pk_points+"] PK point(s).";
	}
	end;
	
	
OnInit:
	set .map$, "guild_vs3";
	set .saved_l, 3; //Length of saved ids can be up to 127.
	set .rstrk_l, 2; //Level of restriction.
	setarray .customs[0],7642,14003,14004,12207,12206,12205,12204,12203,12202; // Enter the ID of customs here...
	setarray .Price[0],15,2,5,3,3,3,3,3,3; // Price for each custom here...
	npcshopdelitem "custom_seller2",501; // Don't touch any coding beyond here..
	for( set .i,0; .customs[.i]; set .i,.i+1 )
		npcshopadditem "custom_seller2",.customs[.i],.Price[.i];
	end;

}
izlude,88,134,5	script	PVP Points Dealer#02212	111,{
end;
OnInit:
	initnpctimer;
	end;
OnTimer1000:
	showscript("PVP Points Seller");
	setnpctimer 0;
	end;
}




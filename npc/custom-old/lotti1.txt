//===== Hercules Script ======================================
//= Lotti Girl
//===== By: ==================================================
//= AnnieRuru
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= Hercules 2019-02-22
//===== Description: ========================================= 
//= Roll a lottery ticket and get some cool items
//= even if you get all the trash items, you still collecting points to buy in lotti shop
//===== Topic ================================================
//= http://herc.ws/board/topic/16579-lotti-girl/
//===== Additional Comments: =================================
//= 
//============================================================

function	script	F_MesItemInfo12	{
	.@item = getarg(0);
	.@itemname$ = getitemname(.@item);
	if (.@itemname$ != "null") {
		.@itemslot = getitemslots(.@item);
		if (.@itemslot)
			.@itemname$ = sprintf("%s [%d]", .@itemname$, .@itemslot);
	}
	else
		.@itemname$ = "Unknown Item";
	if (PACKETVER >= 20150729)
		return sprintf("<ITEM>%s<INFO>%d</INFO></ITEM>", .@itemname$, .@item);
	else if (PACKETVER >= 20130130)
		return sprintf("<ITEMLINK>%s<INFO>%d</INFO></ITEMLINK>", .@itemname$, .@item);
	else
		return .@itemname$;
}

/*
create table lotti_rank (
char_id int(11),
name varchar(24),
lotti_point int(11),
primary key (char_id),
key (lotti_point)
) engine = innodb;
*/

geffen,72,30,6	script	King Poring	471,{
	goto L_talk;
OnPCLoadMapEvent:
	if( strcharinfo(3) == strnpcinfo(4) )
		showevent QTYPE_CLICKME;
	end;
OnInit:

//	Announce to the server when player roll on a rare item
	.rare_announcement = 1; 	// Default 1 : means any item with 0.00%~0.99%

//	Amount of points gain for each roll
	.lotti_points = 1;

//	If the player choose to Roll Repeatedly, what is the maximum amount of Roll in the input
	.max_roll = 100;

	setarray .item,
		7539,			// Item ID requirement
		1,			// amount of the Item needed
		0,			// chance of not getting any items

	//	chance of getting the items
	//	<Item ID>, <amount>, <chance>   
		14003 ,	1,	100,	//esb
		12210  ,1,	100,	//bbg
		14533 ,	1,	100,	//fm
		14592 ,	1,	100,	//jm
		6316 ,	1,	50, 	//token
		12202 ,	1,	100,	//str
		12204 ,	1,	100,	//int
		12205 ,	1,	100,	//dex
		12206 ,	1,	100,	//luk
		12207 ,	1,	100,	//vit
		13697 ,	1,	50,	//bless immortal
		13698,	1,	50,	//agi scroll
		7776 ,	1,	50,	//gympass

		18740,	1,	1,	//ufo
		5914,	1,	1;	//rwc

//		2554 ,	1,	1;	//garb
//		18509,	1,	4,	//blac
//		22654,	1,	100,	//slayer
//		25427,	1,	100,	//even ticket
//		22654,	1,	100,	//slayer glory
//		12213 ,	1,	30,	//neura






	setarray .itemshop,
	//	Item sold in the shop
	//	<Item ID>, <Lotti Points needed>
		14003 ,	2,	//esb
		12411 ,	5,	//HD manual
		12412 ,	5,	//HD BBG
		14004 ,	15, //edp box
		17069,	30,	//acid box
//		22654,	5,	//Seal Card
		7622 ,	35,		//dye hair
		6046,	35,		//cloth
		7539,	4;	//p.coin	

//	=========================================================================================================

	.size = getarraysize(.item);
	for ( .@i = 2; .@i < .size; .@i += 3 )
		.totalchance += .item[.@i];
	for ( .@i = 2; .@i < .size; .@i += 3 ) {
		.@chance = .item[.@i] *10000 / .totalchance;
		.@division = .@chance / 100;
		.@remainder$ = .@chance % 100;
		while ( getstrlen(.@remainder$) < 2 )
			.@remainder$ = insertchar(.@remainder$, "0", 0);
		.chance_display$[.@i] = .@division +"."+ .@remainder$ +"%";
		if ( .@division < .rare_announcement )
			.announce[.@i] = true;
	}
	.shop_size = getarraysize(.itemshop);
	npcshopdelitem "Lotti#hidden", 512;
	for ( .@i = 0; .@i < .shop_size; .@i += 2 )
		npcshopadditem "Lotti#hidden", .itemshop[.@i], .itemshop[.@i +1];
	npcshopattach "Lotti#hidden";
	.npcname$ = "[ "+strnpcinfo(3)+" ]";
	end;
L_talk:
	mes .npcname$;
	mes "Wanna test your luck today?";
	mes "Here's our ^FF0000Grand Prize^000000 for this week.";
	mes "><ITEM>Costume Fluttering Butterfly<INFO>5914</INFO></ITEM>";
	mes "><ITEM>Costume Hair of the Strong<INFO>18740</INFO></ITEM>";
//	mes "><ITEM>RWC2010 Indonesia[1]<INFO>18509</INFO></ITEM>";
//	mes "><ITEM>Ufo Poring Hat[1]<INFO>18892</INFO></ITEM>";



	next;
	switch( select( "^FF0000Roll it!^000000", "Consolation Prize", "King Poring Shop", "Top Spender" ) ) {
	mes .npcname$;
	case 1:
		mes "You need "+.item[1]+"x "+F_MesItemInfo12( .item[0] )+" to play.";
		if ( countitem( .item[0] ) < .item[1] )
			close;
		next;
		if ( select( "Single roll", "Roll repeatedly for 100x" ) == 1 ) {
			mes .npcname$;
			while ( true ) {
				mes "Good luck!...";
				.@r = rand(.totalchance);
				for ( .@i = 2; ( .@r -= .item[.@i] ) >= 0; .@i += 3 );
				if ( checkweight( .item[.@i -2], .item[.@i -1] ) == false ) {
					mes "It appears you have overweight";
					break;
				}
				delitem .item[0], .item[1];
				if ( .@i == 2 )
					mes "You get nothing...";
				else {
					mes "You got "+.item[.@i -1]+"x "+F_MesItemInfo12(.item[.@i -2]);					getitem .item[.@i -2], .item[.@i -1];
					if ( .announce[.@i] )
						announce sprintf (( "Lotti Girl: Player [%s] has roll on [%s](%s)" ), strcharinfo(0), getitemname(.item[.@i -2]), .chance_display$[.@i] ), bc_all,0xFFFF00;
//			announce "Attention Slayers!!! "+strcharinfo(0)+" Won a rare prize ["+getitemname(.item[.@i -2])+"] from Gatchapooooon!!",0;

				}
				lotti_points += .lotti_points;
				query_sql "insert into lotti_rank values ( "+ getcharid(CHAR_ID_CHAR) +", '"+ escape_sql( strcharinfo(PC_NAME) ) +"', "+ .lotti_points +" ) on duplicate key update lotti_point = lotti_point + "+ .lotti_points;
				mes " ";
				mes "wanna try again ?";
				next;
				if ( select( "Yes", "No" ) == 2 ) break;
				mes .npcname$;
				if ( countitem( .item[0] ) < .item[1] ) {
					mes " ";
					mes "It appears you have ran out of "+F_MesItemInfo12( .item[0] );
					break;
				}
			}
		}
		else {
			mes .npcname$;
			mes "Input how many rounds to roll";
			mes "Currently you have "+countitem( .item[0] )+" "+F_MesItemInfo12( .item[0] );
			next;
			input .@roll_input, 1, min( countitem( .item[0] ) / .item[1], .max_roll );
			mes .npcname$;
			mes "Are you sure you want to roll "+.@roll_input+" times ?";
			next;
			if ( select( "Yes", "No" ) == 2 ) close;
			mes .npcname$;
			mes "Here we go...";
			mes " ";
//			freeloop true;
			for ( .@j = 0; .@j < .@roll_input; ++.@j ) {
				.@r = rand(.totalchance);
				for ( .@i = 2; ( .@r -= .item[.@i] ) >= 0; .@i += 3 );
				if ( checkweight( .item[.@i -2], .item[.@i -1] ) == false ) {
					mes "It appears you have overweight";
					break;
				}
				delitem .item[0], .item[1];
				if ( .@i == 2 )
					mes "You get nothing...";
				else {
					mes "You get "+.item[.@i -1]+"x "+F_MesItemInfo12(.item[.@i -2]);
					getitem .item[.@i -2], .item[.@i -1];
					if ( .announce[.@i] )
						//announce sprintf(( "Lotti Girl: Player [%s] has roll on [%s](%s)" ), strcharinfo(0), getitemname(.item[.@i -2]), .chance_display$[.@i] ), bc_all,0xFFFF00;
			announce "Attention ROPIPZ!!! player: "+strcharinfo(0)+" Won the GRAND PRIZE!!! ["+getitemname(.item[.@i -2])+"] from King Poring!!",0;


				}
				++.@roll;
				sleep2 1; // prevent lag the server
			}
			lotti_points += .lotti_points * .@roll;
			query_sql "insert into lotti_rank values ( "+ getcharid(CHAR_ID_CHAR) +", '"+ escape_sql( strcharinfo(PC_NAME) ) +"', "+ .lotti_points +" ) on duplicate key update lotti_point = lotti_point + "+( .lotti_points * .@roll );
			mes "Total roll : "+.@roll+" times.";
		}
		mes " ";
		mes "Thank you come back again~";
		close;
	case 2:
		freeloop true;
		mes "Item Required:<ITEM>Gotcha Poring Coin<INFO>7539 </INFO></ITEM>";
//			mes "Chance to gain ^FF0000nothing^000000 : " + .chance_display$[2];  
		mes "Consolation Prize:";
		mes "><ITEM>Elite Siege Box<INFO>14003 </INFO></ITEM>";
		mes "><ITEM>Bubble Gum<INFO>12210 </INFO></ITEM>";
		mes "><ITEM>Field Manual<INFO>14533 </INFO></ITEM>";
		mes "><ITEM>Job Manual<INFO>14592 </INFO></ITEM>";
		mes "><ITEM>Token of Siegfried<INFO>6316</INFO></ITEM>";
		mes "><ITEM>Steamed Tongue<INFO>12202</INFO></ITEM>";
		mes "><ITEM>Steamed Scorpion<INFO>12203</INFO></ITEM>";
		mes "><ITEM>Dragon Breath Cocktail<INFO>12204</INFO></ITEM>";
		mes "><ITEM>Hwergelmir's Tonic<INFO>12205</INFO></ITEM>";
		mes "><ITEM>Cooked Nine Tail's Tails<INFO>12206</INFO></ITEM>";
		mes "><ITEM>Stew of Immortality<INFO>12207</INFO></ITEM>";
		mes "><ITEM>LV10 Blessing Scroll<INFO>13697</INFO></ITEM>";
		mes "><ITEM>LV10 Agility Scroll<INFO>13698</INFO></ITEM>";
		mes "><ITEM>Gym Pass<INFO>7776 </INFO></ITEM>";
//		mes "><ITEM>Neuralizer<INFO>12213 </INFO></ITEM>";
//		mes "><ITEM>GM Event Ticket<INFO>25427</INFO></ITEM>";
//		mes "><ITEM>Slayer's Glory<INFO>22654</INFO></ITEM>";


		next;
		mes .npcname$;
		mes "Each time you roll, will gain "+.lotti_points+" Poring Points(s).";
		mes "Which can be use to buy items in King Poring Shop.";
		close;
	case 3:
		mes "You currently have ^0000FF"+lotti_points+"^000000 Poring Point(s).";
		dispbottom "You currently have "+lotti_points+" Poring Point(s).";
		close2;
		callshop "Lotti#hidden", 1;
		end;
	case 4:
		.@query$  = "SELECT `name`, IF(@d=t.`lotti_point`, @r, @r:=@i), @d:=t.`lotti_point`, @i:=@i+1 ";
		.@query$ += "FROM `lotti_rank` t, (SELECT @d:=0, @r:=0, @i:=1)q ";
		.@query$ += "ORDER BY `lotti_point` DESC LIMIT 5";
		.@nb = query_sql(.@query$, .@name$, .@rank, .@points, .@dummy);
		if ( !.@nb ) {
			mes "There's no ranking yet ~";
			close;
		}
		for ( .@i = 0; .@i < .@nb; ++.@i )
			mes ""+.@rank[.@i]+". "+.@name$[.@i]+" : "+.@points[.@i]+" Point(s)";
		mes " ";
		if ( !query_sql( "SELECT `lotti_point`, 1+(SELECT COUNT(1) FROM `lotti_rank` t1 WHERE t1.lotti_point > t2.lotti_point) FROM `lotti_rank` t2 WHERE `char_id` = "+ getcharid(CHAR_ID_CHAR), .@points, .@rank ) ) {
			mes "You are not in the rank.";
			close;
		}
		mes "Your current :";
		mes "> Rank : "+.@rank;
		mes "> Point : "+.@points+" Point(s).";
		close;
	}
	end; // shouldn't reach
OnBuyItem:
	mes .npcname$;
	if ( !@bought_quantity ) end;
	if ( checkweight2( @bought_nameid, @bought_quantity ) == false ) {
		mes "It appears you can't carry them all";
		close;
	}
	.@size = getarraysize( @bought_nameid );
	for ( .@i = 0; .@i < .@size; .@i++ ) {
		for ( .@j = 0; .@j < .shop_size; .@j += 2 )
			if ( @bought_nameid[.@i] == .itemshop[.@j] )
				break;
		.@itemcost += .itemshop[.@j +1] * @bought_quantity[.@i];
	}
	if ( .@itemcost > lotti_points ) {
		mes "You don't have enough Poring Points";
		close;
	}
	lotti_points -= .@itemcost;
	for ( .@i = 0; .@i < .@size; ++.@i )
		getitem @bought_nameid[.@i], @bought_quantity[.@i];
	deletearray @bought_nameid;
	deletearray @bought_quantity;
	mes "Thanks for buying! Come back again ~";
	close;
}
-	shop	Lotti#hidden	-1,512:1000